%% Tokens
begin end ID = replace with in ; recursivereplace >! print ( ) , # find diff union inters maxfreqstring REGEX ASCII-STR EPSILON

%% Start
<MiniRE-program>

%% Rules
<MiniRE-program>		begin <statement-list> end
<statement-list>		<statement> <statement-list-tail>
<statement-list-tail>	<statement> <statement-list-tail>
<statement-list-tail>	EPSILON
<statement>				ID = <assignment> ;
<statement>				replace REGEX with ASCII-STR in <file-names> ;
<statement>				recursivereplace REGEX with ASCII-STR in <file-names> ;
<statement>				print ( <exp-list> ) ;
<assignment>			<exp>
<assignment>			# <exp>
<assignment>			maxfreqstring ( ID )
<file-names>			<source-file> >! <destination-file>
<source-file>			ASCII-STR
<destination-file>		ASCII-STR
<exp-list>				<exp> <exp-list-tail>
<exp-list-tail>			, <exp> <exp-list-tail>
<exp-list-tail>			EPSILON
<exp>					ID
<exp>					( <exp> )
<exp>					<term> <exp-tail>
<exp-tail>				<bin-op> <term> <exp-tail>
<exp-tail>				EPSILON
<term>					find REGEX in <file-name>
<file-name>				ASCII-STR
<bin-op>				diff
<bin-op>				union
<bin-op>				inters
